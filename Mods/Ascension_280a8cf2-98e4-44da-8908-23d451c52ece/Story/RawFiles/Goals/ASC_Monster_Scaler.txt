Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_ASC_Orb((ITEMGUID)9f4bea07-e835-4213-850e-3f0f9768d0cb);

DB_ASC_ScalingEnabled(2);

DB_ASC_HighestLevel("HighestLevel", 1, 0, 1);

DB_ASC_ScalingFlag("ASC_DecreaseMonsterDifficulty", -1);
DB_ASC_ScalingFlag("ASC_IncreaseMonsterDifficulty", 1);

DB_ASC_ResetFlag("ASC_ResetMonsterDifficulty");

DB_ASC_ToggleFlag("ASC_ToggleMonsterDifficulty");

DB_ASC_GaveItem(0);
KBSECTION
PROC
	PROC_ASC_GiveOrb()
AND
	DB_IsPlayer(_Character)
AND
	ItemTemplateIsInCharacterInventory(_Character, "ASC_Orb_of_Strife_9f4bea07-e835-4213-850e-3f0f9768d0cb", 0)
THEN
	ItemTemplateAddTo("ASC_Orb_of_Strife_9f4bea07-e835-4213-850e-3f0f9768d0cb", _Character, 1);

PROC
	PROC_ASC_DisplayToggle((INTEGER)_IsEnabled)
AND
	_IsEnabled == 0
AND
	DB_IsPlayer(_Character)
THEN
	CharacterStatusText(_Character, "Monster Scaling Disabled!");

PROC
	PROC_ASC_DisplayToggle((INTEGER)_IsEnabled)
AND
	_IsEnabled == 1
AND
	DB_IsPlayer(_Character)
THEN
	CharacterStatusText(_Character, "Monster Scaling Enabled!");

PROC
	PROC_ASC_DisplayDifficulty((INTEGER)_Difficulty)
AND
	_Difficulty > 0
AND
	IntegertoString(_Difficulty, _StringDifficulty)
AND
	StringConcatenate("Monster Scaling set to +", _StringDifficulty, _Message)
AND
	DB_IsPlayer(_Character)
THEN
	CharacterStatusText(_Character, _Message);

PROC
	PROC_ASC_DisplayDifficulty((INTEGER)_Difficulty)
AND
	_Difficulty == 0
AND
	DB_IsPlayer(_Character)
THEN
	CharacterStatusText(_Character, "Monster Scaling set to 0");

PROC
	PROC_ASC_DisplayDifficulty((INTEGER)_Difficulty)
AND
	_Difficulty < 0
AND
	IntegertoString(_Difficulty, _StringDifficulty)
AND
	StringConcatenate("Monster Scaling set to ", _StringDifficulty, _Message)
AND
	DB_IsPlayer(_Character)
THEN
	CharacterStatusText(_Character, _Message);

PROC
	PROC_ASC_AlterScalingDifficulty((STRING)_Flag)
AND
	DB_ASC_ScalingFlag(_Flag, _ChangeMod)
AND
	DB_ASC_HighestLevel("HighestLevel", _Level, _CurrentMod, _Total)
AND
	IntegerSum(_CurrentMod, _ChangeMod, _NewMod)
AND
	IntegerSum(_Level, _NewMod, _NewTotal)
THEN
	NOT DB_ASC_HighestLevel("HighestLevel", _Level, _CurrentMod, _Total);
	DB_ASC_HighestLevel("HighestLevel", _Level, _NewMod, _NewTotal);
	PROC_ASC_DisplayDifficulty(_NewMod);
	GlobalClearFlag(_Flag);

PROC
	PROC_ASC_ResetScalingDifficulty((STRING)_Flag)
AND
	DB_ASC_ResetFlag(_Flag)
AND
	DB_ASC_HighestLevel("HighestLevel", _Level, _CurrentMod, _Total)
THEN
	NOT DB_ASC_HighestLevel("HighestLevel", _Level, _CurrentMod, _Total);
	DB_ASC_HighestLevel("HighestLevel", _Level, 0, _Level);
	PROC_ASC_DisplayDifficulty(0);
	GlobalClearFlag(_Flag);

PROC
	PROC_ASC_ToggleScalingDifficulty((STRING)_Flag)
AND
	DB_ASC_ToggleFlag(_Flag)
AND
	DB_ASC_ScalingEnabled(_IsEnabled)
AND
	IntegerSum(_IsEnabled, 1, _ModMe)
AND
	IntegerModulo(_ModMe, 2, _NewIsEnabled)
THEN
	NOT DB_ASC_ScalingEnabled(_IsEnabled);
	DB_ASC_ScalingEnabled(_NewIsEnabled);
	PROC_ASC_DisplayToggle(_NewIsEnabled);


PROC
	PROC_ASC_UpdateLevel()
AND
	DB_IsPlayer(_Character)
AND
	CharacterGetLevel(_Character, _Level)
AND
	DB_ASC_HighestLevel("HighestLevel", _HighestLevel, _Mod, _LevelScale)
AND
	_Level > _HighestLevel
AND
	IntegerSum(_Level, _Mod, _NewScale)
THEN
	NOT DB_ASC_HighestLevel("HighestLevel", _HighestLevel, _Mod, _LevelScale);
	DB_ASC_HighestLevel("HighestLevel", _Level, _Mod, _NewScale);

PROC
	PROC_ASC_EnableScaling()
AND
	DB_ASC_ScalingEnabled(_IsEnabled)
AND
	_IsEnabled == 2
THEN
	GlobalSetFlag("ASC_ToggleMonsterDifficulty");


IF
	ObjectEnteredCombat((CHARACTERGUID)_Character, _)
AND
	DB_ASC_ScalingEnabled(1)
AND
	NOT DB_IsPlayer(_Character)
AND
	CharacterGetLevel(_Character, _Level)
AND
	DB_ASC_HighestLevel("HighestLevel", _, _, _HighestLevel)
AND
	_Level < _HighestLevel
THEN
	CharacterLevelUpTo(_Character, _HighestLevel);

IF
	CharacterUsedItemTemplate(_Character, "ASC_Orb_of_Strife_9f4bea07-e835-4213-850e-3f0f9768d0cb", _Item)
THEN
	Proc_StartDialog(0, "ASC_OrbOfStrife", _Item, _Character);


IF
	SavegameLoaded(_,_,_,_)
THEN
	PROC_ASC_GiveOrb();
	PROC_ASC_UpdateLevel();
	PROC_ASC_EnableScaling();

IF
	RegionStarted(_)
THEN
	PROC_ASC_GiveOrb();
	PROC_ASC_UpdateLevel();
	PROC_ASC_EnableScaling();

IF
	CharacterLeveledUp(_Character)
THEN
	PROC_ASC_UpdateLevel();

IF
	GlobalFlagSet(_Flag)
THEN
	PROC_ASC_ToggleScalingDifficulty(_Flag);
	PROC_ASC_AlterScalingDifficulty(_Flag);
	PROC_ASC_ResetScalingDifficulty(_Flag);

IF
	GlobalFlagCleared(_Flag)
THEN
	PROC_ASC_ToggleScalingDifficulty(_Flag);
EXITSECTION

ENDEXITSECTION
